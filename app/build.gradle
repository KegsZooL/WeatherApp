plugins {
    id 'com.android.application'
}

def loadEnv() {
    def env = [:]
    def envFile = rootProject.file(".env")
    if (envFile.exists()) {
        envFile.eachLine { line ->
            def trimmed = line.trim()
            if (trimmed && !trimmed.startsWith("#") && trimmed.contains("=")) {
                def delimiterIndex = trimmed.indexOf("=")
                def key = trimmed.substring(0, delimiterIndex).trim()
                def value = trimmed.substring(delimiterIndex + 1).trim()
                env[key] = value
            }
        }
    }
    return env
}

def env = loadEnv()
def openWeatherApiKeyRaw = env["OPEN_WEATHER_API_KEY"] ?: System.getenv("OPEN_WEATHER_API_KEY") ?: ""
def openWeatherApiKey = openWeatherApiKeyRaw.replace("\\", "\\\\").replace("\"", "\\\"")

android {
    compileSdk 36

    defaultConfig {
        applicationId "com.topurayhan.weather"
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "OPEN_WEATHER_API_KEY", "\"${openWeatherApiKey}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.kegszool.weather'
}

dependencies {

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.koushikdutta.ion:ion:3.0.8'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "org.jetbrains.kotlin") {
            details.useVersion("1.8.22")
        }
    }
}